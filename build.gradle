plugins {
    id 'java'
    id 'io.quarkus'
    id 'jacoco'
    id "org.sonarqube" version '3.3'
}

repositories {
    mavenCentral()
    mavenLocal()
}

[
        "tests"
].each {
    apply from: "gradle/${it}.gradle"
}

dependencies {
    implementation enforcedPlatform("$project.quarkusPlatformGroupId:$project.quarkusPlatformArtifactId:$project.quarkusPlatformVersion")

    implementation "io.quarkus:quarkus-hibernate-orm",
            "io.quarkus:quarkus-qute",
            "io.quarkus:quarkus-jdbc-mariadb",
            "io.quarkus:quarkus-resteasy",
            "io.quarkus:quarkus-agroal",
            "io.quarkus:quarkus-resteasy-qute",
            "io.quarkus:quarkus-arc",
            "io.quarkus:quarkus-test-common",
            "io.quarkus:quarkus-security-jpa",
            "io.quarkus:quarkus-hibernate-validator:$project.hibernateVersion",
            "io.vavr:vavr:$project.vavrVersion",
            "org.projectlombok:lombok:$project.lombokVersion",
            "com.google.guava:guava:$project.guavaVersion"

    annotationProcessor "org.projectlombok:lombok:$project.lombokVersion"
}

jacoco {
    toolVersion = "0.8.8"
    reportsDir = file("${buildDir}/jacoco")
}

task mergeJacocoReports(type: JacocoReport) {
    dependsOn test
    sourceDirectories.setFrom files(sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(sourceSets.main.output)
    executionData(
            file("${buildDir}/jacoco/test.exec")
    )
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination file("${buildDir}/jacoco/merge")
        xml.destination file("${buildDir}/jacoco/merge.xml")
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "justinhada_StatementOfQosts"
        property "sonar.organization", "justinhada"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", System.getenv("SONAR_TOKEN")
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/jacoco/merge.xml"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

group 'de.justinharder.soq'
version '1.0.1'
